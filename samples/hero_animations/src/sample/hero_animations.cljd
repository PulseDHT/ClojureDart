(ns sample.hero-animations
  "Faithful port of https://docs.flutter.dev/cookbook/navigation/hero-animations"
  (:require ["package:flutter/material.dart" :as m]
            [cljd.flutter.alpha :as f]))

(def detail-screen
  (f/widget
    :inherit [m/Navigator]
    (m/Scaffold
      :body
      (f/nest
        (m/GestureDetector
          :onTap #(.pop navigator))
        m/Center
        (m/Hero :tag "imageHero")
        (m.Image/network "https://picsum.photos/250?image=9")))))

(def main-screen
  (f/widget
    :inherit [m/Navigator]
    (m/Scaffold
      :appBar (m/AppBar :title (m/Text "Main Screen"))
      :body
      (m/GestureDetector
        :onTap #(do
                  (.push navigator (#/(m/MaterialPageRoute Object) :builder (fn [_] detail-screen)))
                  nil)
        :child  (m/Hero
                  :tag "imageHero"
                  :child (m.Image/network "https://picsum.photos/250?image=9"))))))



(defn main
  []
  (m/runApp
    (m/MaterialApp
      :home main-screen
      :debugShowCheckedModeBanner false)))

